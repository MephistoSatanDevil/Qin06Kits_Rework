
DEFINE_ACTION_FUNCTION styles BEGIN

	//小图标
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@24) STR_VAR bam_file=c4QAM1d RET d1=icon END
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@25) STR_VAR bam_file=c4QAM2d RET d2=icon END
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@26) STR_VAR bam_file=c4QAM3d RET d3=icon END
	
	//禁APR武器, 远程惩罚
	LAF CREATE_EFFECT INT_VAR opcode=362 target=1 timing=0 duration=600 parameter1=18 STR_VAR name=c4QANoRW END
	
	COPY ~%ITEMS%/c4QAM0.itm~ ~%workspace%~
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=249 target=1 timing=2 STR_VAR resource=c4QANoRW END
		PATCH_FOR_EACH type IN 15 18 24 27 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=249 opcode=181 parameter1=type parameter2=0 special= RESOLVE_STR_REF (@1) END
		END
		INNER_ACTION BEGIN
			COPY_EXISTING_REGEXP - GLOB ~.*\.itm~ override
				SET APR=0
				GET_OFFSET_ARRAY effect ITM_V10_GEN_EFFECTS
				PHP_EACH effect AS int => offset BEGIN
					READ_SHORT offset opcode
					READ_LONG offset+4 param1
					READ_LONG offset+8 param2
					PATCH_IF opcode=1 && ((param1>0 && param2=0) || (param1>1 && param2=1)) BEGIN
						SET APR=1
					END
				END
				PATCH_IF APR BEGIN
					GET_OFFSET_ARRAY headers ITM_V10_HEADERS
					PHP_EACH headers AS int => index BEGIN
						READ_BYTE index type
						PATCH_IF type=1 BEGIN
							SET $APR_WEAPON(~%SOURCE_RES%~)=1
						END
					END
				END
		END
		PHP_EACH APR_WEAPON AS item => int BEGIN
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=180 target=1 timing=2 parameter1= RESOLVE_STR_REF (@0)  STR_VAR resource=~%item%~ END
		END

	//戒指
	OUTER_FOR (i=0;i<4;++i) BEGIN

		COPY ~%workspace%/c4QAM0.itm~ ~override/c4QAM%i%.itm~
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM1 name=~@24~ RET h1=new_header END
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM2 name=~@25~ RET h2=new_header END
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM3 name=~@26~ RET h3=new_header END
			FOR (j=1;j<4;++j) BEGIN
				SET name=j+23
				SET name= RESOLVE_STR_REF ((AT name))
				LPF ADD_ITEM_EFFECT INT_VAR header=~h%j%~ opcode=143 target=1 timing=1 parameter1=7 STR_VAR resource=~c4QAM%j%~ END
				LPF ADD_ITEM_EFFECT INT_VAR header=~h%j%~ opcode=139 target=1 timing=1 parameter1=name END
			END
			LPF ADD_ITEM_EFFECT INT_VAR opcode=215 target=1 timing=0 duration=2 parameter2=1 STR_VAR resource=SPCHMPSI END
			PATCH_IF i=0 BEGIN
				SAY 0x08 @21
				SAY 0x0c @21
				SAY 0x50 @22
				SAY 0x54 @22
			END ELSE BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=142 target=1 timing=2 parameter2=~d%i%~ END
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=272 target=1 timing=2 parameter1=1 parameter2=3 STR_VAR resource=~c4QAM%i%0~ END
				FOR (j=1;j<4;++j) BEGIN
					SET name= i+23
					SET name= RESOLVE_STR_REF ((AT name))
					WRITE_LONG 0x08 name
					WRITE_LONG 0x0c name
					SAY 0x50 @23
					SAY 0x54 @23
				END
			END
			
	END
	
	OUTER_FOR (i=1;i<4;++i) BEGIN
		//戒指上重复生效效果
		LAF CREATE_EFFECT INT_VAR opcode=146 target=1 duration=1 parameter2=1 STR_VAR resource=~c4QAM%i%0~ name=~c4QAM%i%0~ END
	END
	
	////武器姿态
	COPY_EXISTING c4QAM1.itm override
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM11 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM21 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM22 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM31 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=146 timing=9 parameter2=1 STR_VAR resource=c4QAM11 END
	
	COPY ~%SPELLS%/c4QAM10.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=2	level_multiplier=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=1 multi_parameter1=1 END
		GET_OFFSET_ARRAY headers SPL_V10_HEADERS
		PHP_EACH headers AS int => index BEGIN
			READ_SHORT index+0x10 min_level
			SET thac0 = (min_level+2)/3
			GET_OFFSET_ARRAY2 effect index SPL_V10_HEAD_EFFECTS
			PHP_EACH effect AS int => offset BEGIN
				READ_SHORT offset opcode
				PATCH_IF opcode=278 BEGIN
					WRITE_LONG offset+0x4 thac0
				END
			END
		END
		
	COPY ~%SPELLS%/c4QAM1A.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=3  END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=50 multi_parameter1=5 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@31) END

	//高阶
	COPY_EXISTING c4QAM1A.eff ~override/c4QAM1AX.eff~
		WRITE_ASCII 0x30 c4QAM1AX
	COPY_EXISTING c4QAM1B.eff ~override/c4QAM1BX.eff~
		WRITE_ASCII 0x30 c4QAM1BX
		
	// COPY ~%SPELLS%/c4QAM1A.spl~ ~override/c4QAM1AX.spl~
	COPY_EXISTING c4QAM1A.spl ~override/c4QAM1AX.spl~
		// LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=3  END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=100 multi_parameter1=10 END
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM1B~ resource=~c4QAM1BX~ END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@31) END
	
	COPY_EXISTING c4QAM1B.spl ~override/c4QAM1BX.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM1A~ resource=~c4QAM1AX~ END
		
	////狂暴姿态
	COPY_EXISTING c4QAM2.itm override
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM11 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM21 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM22 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM31 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=146 timing=1 parameter2=1 STR_VAR resource=c4QAM21 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=146 timing=1 parameter2=1 STR_VAR resource=c4QAM22 END
	
	COPY ~%SPELLS%/c4QAM20.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=250 parameter1=1 multi_parameter1=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=301 parameter1=1 multi_parameter1=1 END
		
	COPY ~%SPELLS%/c4QAM2A.spl~ override
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@33) END
		
	COPY ~%SPELLS%/c4QAM2B.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=285 duration=6 multi_duration=3 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@34) END
		
	COPY_EXISTING INAREANP.pro ~override/c4QAM2C.pro~
		WRITE_SHORT 0x204 43
		WRITE_SHORT 0x206 43
		
	ADD_PROJECTILE ~override/c4QAM2C.pro~
		
	COPY ~%SPELLS%/c4QAM2C.spl~ override
		LPF ALTER_SPELL_HEADER INT_VAR projectile=c4QAM2C END
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=12 parameter1=1 multi_parameter1=1 END
		
	//高阶
	COPY_EXISTING c4QAM2A.eff ~override/c4QAM2AX.eff~
		WRITE_ASCII 0x30 c4QAM2AX

	COPY_EXISTING c4QAM2B.eff ~override/c4QAM2BX.eff~
		WRITE_ASCII 0x30 c4QAM2BX

	COPY_EXISTING c4QAM2A.spl ~override/c4QAM2AX.spl~
		// LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM2C~ resource=~c4QAM2CX~ END
	COPY_EXISTING c4QAM2B.spl ~override/c4QAM2BX.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=285 parameter1=2 END
		
	/* 找到无法屏蔽低级旋风的原因之前使用两次伤害 
	COPY ~%SPELLS%/c4QAM2C.spl~ ~override/c4QAM2CX.spl~
		LPF ALTER_SPELL_HEADER INT_VAR projectile=c4QAM2C END
		LPF ALTER_EFFECT INT_VAR dicenumber=2 END
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=12 parameter1=2 multi_parameter1=2 END
	*/
		
	////防御姿态
	COPY_EXISTING c4QAM3.itm override
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM11 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM21 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM22 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=321 target=1 timing=0 duration=0 parameter2=0 STR_VAR resource=c4QAM31 END
		LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=146 timing=1 parameter2=1 STR_VAR resource=c4QAM31 END
	
	COPY ~%SPELLS%/c4QAM30.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=0 parameter1=2 multi_parameter1=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=166 parameter1=10 multi_parameter1=5 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=73 parameter1=~-4~ multi_parameter1=~-2~ END
		
	COPY ~%SPELLS%/c4QAM3A.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=10	level_multiplier=10 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=218 parameter1=1 multi_parameter1=1 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@35) END

	//高阶
	COPY ~%SPELLS%/c4QAM3A.spl~ ~override/c4QAM3AX.spl~
		
	////防叠加, 切换移除
	COPY_EXISTING_REGEXP GLOB ~c4QAM[123]0.spl~ override
		FOR (i=1;i<4;++i) BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM%i%0~ END
		END
		
	COPY_EXISTING c4QAM11.spl override
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM2AX END
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM2BX END
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM3AX END
		
	COPY_EXISTING_REGEXP GLOB ~c4QAM2[12].spl~ override
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM1AX END
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM3AX END
		
	COPY_EXISTING c4QAM31.spl override
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM1AX END
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM2AX END
		LPF CLONE_EFFECT INT_VAR match_opcode=248 opcode=206 STR_VAR resource=c4QAM2BX END

END
