
DEFINE_ACTION_FUNCTION styles BEGIN

	//小图标
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@24) STR_VAR bam_file=c4QAM1d RET d1=icon END
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@25) STR_VAR bam_file=c4QAM2d RET d2=icon END
	LAF cd_new_portrait_icon INT_VAR string= RESOLVE_STR_REF (@26) STR_VAR bam_file=c4QAM3d RET d3=icon END
	
	//使用超渡等级作为高阶判断
	LAF ADD_SPLPROT_ENTRY INT_VAR stat=55 value=~-1~ relation=1 RET TDLEVEL=index END
	
	//禁APR武器, 远程惩罚
	LAF CREATE_EFFECT INT_VAR opcode=362 target=1 timing=0 duration=600 parameter1=18 STR_VAR name=c4QANoRW END
	
	COPY ~%ITEMS%/c4QAM0.itm~ ~%workspace%~
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode=249 target=1 timing=2 STR_VAR resource=c4QANoRW END
		PATCH_FOR_EACH type IN 15 18 24 27 BEGIN
			LPF CLONE_EFFECT INT_VAR match_opcode=249 opcode=181 parameter1=type parameter2=0 special= RESOLVE_STR_REF (@1) END
		END
		INNER_ACTION BEGIN
			COPY_EXISTING_REGEXP - GLOB ~.*\.itm~ override
				SET APR=0
				GET_OFFSET_ARRAY effect ITM_V10_GEN_EFFECTS
				PHP_EACH effect AS int => offset BEGIN
					READ_SHORT offset opcode
					READ_LONG offset+4 param1
					READ_LONG offset+8 param2
					PATCH_IF opcode=1 && ((param1>0 && param2=0) || (param1>1 && param2=1)) BEGIN
						SET APR=1
					END
				END
				PATCH_IF APR BEGIN
					GET_OFFSET_ARRAY headers ITM_V10_HEADERS
					PHP_EACH headers AS int => index BEGIN
						READ_BYTE index type
						PATCH_IF type=1 BEGIN
							SET $APR_WEAPON(~%SOURCE_RES%~)=1
						END
					END
				END
		END
		PHP_EACH APR_WEAPON AS item => int BEGIN
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=180 target=1 timing=2 parameter1= RESOLVE_STR_REF (@0)  STR_VAR resource=~%item%~ END
		END

	//戒指
	OUTER_FOR (i=0;i<4;++i) BEGIN

		COPY ~%workspace%/c4QAM0.itm~ ~override/c4QAM%i%.itm~
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM1 name=~@24~ RET h1=new_header END
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM2 name=~@25~ RET h2=new_header END
			LPF ADD_ITEM_HEADER INT_VAR target=5 STR_VAR icon=c4QAM3 name=~@26~ RET h3=new_header END
			FOR (j=1;j<4;++j) BEGIN
				LPF ADD_ITEM_EFFECT INT_VAR header=~h%j%~ opcode=143 target=2 timing=1 parameter1=7 STR_VAR resource=~c4QAM%j%~ END
			END
			LPF ADD_ITEM_EFFECT INT_VAR opcode=215 target=2 timing=0 duration=2 parameter2=1 STR_VAR resource=SPCHMPSI END
			PATCH_IF i=0 BEGIN
				SAY 0x08 @21
				SAY 0x0c @21
				SAY 0x50 @22
				SAY 0x54 @22
			END ELSE BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=142 target=1 timing=2 parameter2=~d%i%~ END
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=272 target=1 timing=2 parameter1=1 parameter2=3 STR_VAR resource=~c4QAM%i%0~ END
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=272 target=1 timing=2 parameter1=1 parameter2=3 STR_VAR resource=~c4QAM%i%X~ END
				PATCH_IF i=2 BEGIN
					LPF ADD_ITEM_EQEFFECT INT_VAR opcode=272 target=1 timing=2 parameter1=1 parameter2=3 STR_VAR resource=~c4QAM%i%Y~ END
				END
				FOR (j=1;j<4;++j) BEGIN
					SET name= i+23
					SET name= RESOLVE_STR_REF ((AT name))
					WRITE_LONG 0x08 name
					WRITE_LONG 0x0c name
					SAY 0x50 @23
					SAY 0x54 @23
				END
			END
			
	END
	
	//重复生效效果
	OUTER_FOR (i=1;i<4;++i) BEGIN
		LAF CREATE_EFFECT INT_VAR opcode=146 target=2 duration=1 parameter2=1 STR_VAR resource=~c4QAM%i%0~ name=~c4QAM%i%0~ END
		LAF CREATE_EFFECT INT_VAR opcode=326 target=2 duration=1 parameter1=i*10 parameter2=TDLEVEL STR_VAR resource=~c4QAM%i%X~ name=~c4QAM%i%X~ END
		ACTION_IF i=2 BEGIN
			LAF CREATE_EFFECT INT_VAR opcode=326 target=2 duration=1 parameter1=i*10+1 parameter2=TDLEVEL STR_VAR resource=~c4QAM%i%Y~ name=~c4QAM%i%Y~ END
		END
	END
	
	//武器姿态
	COPY ~%SPELLS%/c4QAM10.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=278 parameter1=1 multi_parameter1=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=20 multi_parameter1=10 END
		
	COPY ~%SPELLS%/c4QAM1A.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=3  END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=100 multi_parameter1=10 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@31) END
		
	COPY ~%SPELLS%/c4QAM1B.spl~ override
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@32) END
		
	//狂暴姿态
	COPY ~%SPELLS%/c4QAM20.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=250 parameter1=1 multi_parameter1=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=301 parameter1=1 multi_parameter1=1 END
		
	COPY ~%SPELLS%/c4QAM2A.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=285 duration=6 multi_duration=3 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@33) END
		
	COPY ~%SPELLS%/c4QAM2B.spl~ override
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@34) END
		
	COPY_EXISTING INAREANP.pro ~override/c4QAM2C.pro~
		WRITE_SHORT 0x204 43
		WRITE_SHORT 0x206 43
		
	ADD_PROJECTILE ~override/c4QAM2C.pro~
		
	COPY ~%SPELLS%/c4QAM2C.spl~ override
		LPF ALTER_SPELL_HEADER INT_VAR projectile=c4QAM2C END
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=12 parameter1=1 multi_parameter1=1 END
		
	//防御姿态
	COPY ~%SPELLS%/c4QAM30.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=8	level_multiplier=7 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=0 parameter1=2 multi_parameter1=1 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=166 parameter1=10 multi_parameter1=5 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=73 parameter1=~-4~ multi_parameter1=~-2~ END
		
	COPY ~%SPELLS%/c4QAM3A.spl~ override
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=10	level_multiplier=10 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=218 parameter1=1 multi_parameter1=1 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@35) END
		
	//防叠加, 切换移除
	COPY_EXISTING_REGEXP GLOB ~c4QAM[123]0.spl~ override
		FOR (i=1;i<4;++i) BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM%i%0~ END
			LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM%i%X~ END
			PATCH_IF i=2 BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM%i%Y~ END
			END
		END
		PATCH_IF ~%SOURCE_RES%~ STRING_CONTAINS_REGEXP ~c4QAM1~ BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM1AX~ END
			LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=1 STR_VAR resource=~c4QAM1A~ END
		END
		
	//高阶后升级
	//武器
	COPY_EXISTING c4QAM10.spl ~override/c4QAM1X.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM1A~ resource=~c4QAM1AX~ END

	COPY_EXISTING c4QAM1A.eff ~override/c4QAM1AX.eff~
		WRITE_ASCII 0x30 c4QAM1AX
		
	COPY ~%SPELLS%/c4QAM1A.spl~ ~override/c4QAM1AX.spl~
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=3  END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=332 parameter1=200 multi_parameter1=20 END
		LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1= RESOLVE_STR_REF (@31) END
	
	COPY ~%SPELLS%/c4QAM1B.spl~ override
		LPF CLONE_EFFECT STR_VAR match_resource=~c4QAM1A~ resource=~c4QAM1AX~ END

	//狂暴
	COPY_EXISTING c4QAM20.spl ~override/c4QAM2X.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM2B~ resource=~c4QAM2BX~ END
	COPY_EXISTING c4QAM2X.spl ~override/c4QAM2Y.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM2A~ resource=~c4QAM2AX~ END

	COPY_EXISTING c4QAM2B.eff ~override/c4QAM2BX.eff~
		WRITE_ASCII 0x30 c4QAM2BX
	COPY_EXISTING c4QAM2A.eff ~override/c4QAM2AX.eff~
		WRITE_ASCII 0x30 c4QAM2AX

	COPY_EXISTING c4QAM2A.spl ~override/c4QAM2AX.spl~
		LPF ALTER_EFFECT INT_VAR match_opcode=285 parameter1=2 END
	COPY_EXISTING c4QAM2B.spl ~override/c4QAM2BX.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM2C~ resource=~c4QAM2CX~ END
		
	COPY ~%SPELLS%/c4QAM2C.spl~ ~override/c4QAM2CX.spl~
		LPF ALTER_SPELL_HEADER INT_VAR projectile=c4QAM2C END
		LPF ALTER_EFFECT INT_VAR dicenumber=2 END
		LPF CREATE_HEADERS_AS_LEVEL INT_VAR level_increment=4	level_multiplier=2 END
		LPF ALTER_HEADER_EFFECTS_AS_LEVEL INT_VAR match_opcode=12 parameter1=2 multi_parameter1=2 END

	//防御
	COPY_EXISTING c4QAM30.spl ~override/c4QAM3X.spl~
		LPF ALTER_EFFECT STR_VAR match_resource=~c4QAM3A~ resource=~c4QAM3AX~ END

	COPY_EXISTING c4QAM3A.eff ~override/c4QAM3AX.eff~
		WRITE_SHORT 0x2e THIS - 15
		
	//防护未升级版本
	OUTER_FOR (i=1;i<4;++i) BEGIN
		COPY_EXISTING_REGEXP GLOB ~c4QAM%i%[XY].spl~ override
			LPF CLONE_EFFECT INT_VAR check_global=0 multi_match=1 match_opcode=321 opcode=206 duration=1 STR_VAR resource=~c4QAM%i%0~ END
	END
	

END
